apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.vipabc.im.mytestapplication"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    // 签名配置
    signingConfigs {
        debug {
            loadSigningConfigs('debug', '../keystore/signing.release.properties')
        }
        release {
            loadSigningConfigs('release', '../keystore/signing.release.properties')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// 加载签名配置
def loadSigningConfigs(type, path) {
    def Properties props = new Properties()
    def propFile = file(path)

    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs."$type".storeFile = file(props['STORE_FILE'])
            android.signingConfigs."$type".storePassword = props['STORE_PASSWORD']
            android.signingConfigs."$type".keyAlias = props['KEY_ALIAS']
            android.signingConfigs."$type".keyPassword = props['KEY_PASSWORD']
        } else {
            android.buildTypes."$type".signingConfig = null
        }
    } else {
        android.buildTypes."$type".signingConfig = null
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.0.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.9'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.android.support:cardview-v7:27.1.1'
}
